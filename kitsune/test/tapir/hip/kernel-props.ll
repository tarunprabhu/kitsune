; Check that the kernel properties pass updates the initializer of the kernel
; properties global variable. This intentionally does not check that the
; contents of the computed metadata are correct.
;
; RUN: opt --tapir=hip --tapir-hip-arch=gfx906 -S %s \
; RUN:     -passes='tapir-lowering<O2>,kit-kernel-properties' \
; RUN:     | FileCheck %s
;
; CHECK: @{{.+}} = private unnamed_addr constant {{.+}} { {{.+}} } #[[KERNEL_PROPS:[0-9]+]]
; CHECK: attributes #[[KERNEL_PROPS]] = {
; CHECK-SAME: "kit_kernel_props"="__kithip_loop_{{.+}}"

target triple = "x86_64-pc-linux-gnu"

define void @f(ptr %c, i64 %n) {
entry:
  %syncreg = tail call token @llvm.syncregion.start()
  %cmp5 = icmp sgt i64 %n, 0
  br i1 %cmp5, label %preheader, label %forall.sync

preheader:
  br label %forall.detach

forall.detach:
  %indvars.iv = phi i64 [ 0, %preheader ], [ %indvars.iv.next, %forall.inc ]
  %indvars.iv.next = add nuw nsw i64 %indvars.iv, 1
  detach within %syncreg, label %forall.body, label %forall.inc

forall.body:
  %arrayidx = getelementptr inbounds i32, ptr %c, i64 %indvars.iv
  store i64 %n, ptr %arrayidx, align 4
  reattach within %syncreg, label %forall.inc

forall.inc:
  %exitcond.not = icmp eq i64 %indvars.iv.next, %n
  br i1 %exitcond.not, label %forall.sync, label %forall.detach, !llvm.loop !0

forall.sync:
  sync within %syncreg, label %forall.end

forall.end:
  ret void
}

!0 = distinct !{!0, !1, !2, !3}
!1 = !{!"tapir.loop.spawn.strategy", i32 1}
!2 = !{!"tapir.loop.target", i32 4}
!3 = !{!"llvm.loop.unroll.disable"}
