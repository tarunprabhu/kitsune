; Check that any constant global variables are handled correctly. They should
; not be copied memcpy'ed, and they should not be registered with the runtime.
;
; RUN: opt --tapir=cuda -passes='tapir-lowering<O2>,kit-ctors' \
; RUN:     -S %s \
; RUN:     | FileCheck %s
;
; CHECK-DAG: @[[FB:.+]] = constant {{.+}} #[[FBATTR:[0-9]+]]
;
; CHECK: define {{.+}} @f
; CHECK-NOT: llvm.kit.symbol.device.ptr
; CHECK-NOT: llvm.kit.symbol.memcpy.htod
; CHECK: %[[TS:.+]] = call {{.+}} @llvm.kit.async.launch.kernel(i32 2, ptr nonnull @[[FB]],
; CHECK-NOT: llvm.kit.symbol.memcpy.host
;
; CHECK: define {{.+}} @.kitcuda.ctor{{[^(]*}}
; CHECK: call {{.+}} @__cudaRegisterFatBinary
; CHECK-NOT: call {{.+}} @__cudaRegisterVar
; CHECK: call {{.+}} @__cudaRegisterFatBinaryEnd
;
; CHECK: #[[FBATTR]] = { kit_fb kit_tt(2) }

target triple = "x86_64-unknown-linux-gnu"

@v137 = external constant i32, align 4

define void @f(ptr %c, i64 %n) {
entry:
  %syncreg = tail call token @llvm.syncregion.start()
  %cmp4.not = icmp eq i64 %n, 0
  br i1 %cmp4.not, label %forall.sync, label %forall.detach

forall.detach:                                    ; preds = %entry, %forall.inc
  %i.05 = phi i64 [ %inc, %forall.inc ], [ 0, %entry ]
  detach within %syncreg, label %forall.body, label %forall.inc

forall.body:                                      ; preds = %forall.detach
  %0 = load i32, ptr @v137, align 4
  %arrayidx = getelementptr inbounds nuw i32, ptr %c, i64 %i.05
  store i32 %0, ptr %arrayidx, align 4
  reattach within %syncreg, label %forall.inc

forall.inc:                                       ; preds = %forall.body, %forall.detach
  %inc = add nuw i64 %i.05, 1
  %exitcond.not = icmp eq i64 %inc, %n
  br i1 %exitcond.not, label %forall.sync, label %forall.detach, !llvm.loop !0

forall.sync:                                      ; preds = %forall.inc, %entry
  sync within %syncreg, label %forall.end

forall.end:                                       ; preds = %forall.sync
  ret void
}

!0 = distinct !{!0, !1, !2}
!1 = !{!"tapir.loop.spawn.strategy", i32 1}
!2 = !{!"llvm.loop.unroll.disable"}
